' Part of "Red Ink for Word"
' Copyright (c) LawDigital Ltd., Switzerland. All rights reserved. For license to use see https://redink.ai.

' =============================================================================
' File: ThisAddIn.Helpers.vb
' Purpose: Provides utility helper methods for the Red Ink for Word add-in,
'          including undo scope management, escape sequence handling, document
'          gathering, text interpolation, progress tracking, API key encoding,
'          and VBA module validation.
'
' Architecture:
'  - WordUndoScope: Manages Word custom undo records for VSTO operations (Word 2013+).
'  - Escape Handling: HideEscape/UnHideEscape convert special characters to/from Unicode sequences.
'  - Document Gathering: GatherSelectedDocuments collects open Word documents with UI selection.
'  - Text Search: FindLongTextInChunks delegates to WordSearchHelper for anchored text searches.
'  - Word Counting: GetSelectedTextLength uses regex to count real words (letters with internal punctuation).
'  - Runtime Interpolation: InterpolateAtRuntime replaces placeholders with instance field/property values.
'  - ProgressScope: IDisposable wrapper for modeless progress windows with cancellation support.
'  - Language Detection: GetWordDefaultInterfaceLanguage retrieves Word UI language via CultureInfo.
'  - API Key Encoding: CodeAPIKey/DeCodeAPIKey encrypt/decrypt API keys with optional prefix handling.
'  - VBA Validation: VBAModuleWorking checks if required VBA helper module meets minimum version.
'  - Win32 Interop: GetAsyncKeyState for keyboard state detection (VK_ESCAPE).
'
' Dependencies: SharedLibrary.SharedMethods, WordSearchHelper, ProgressBarModule, 
'               DPIProgressForm/ProgressForm.
' =============================================================================

Option Explicit On
Option Strict On

Imports System.Diagnostics
Imports System.Globalization
Imports System.Text.RegularExpressions
Imports System.Windows.Forms
Imports Microsoft.Office.Core
Imports Microsoft.Office.Interop.PowerPoint
Imports Microsoft.Office.Interop.Word
Imports NetOffice.PowerPointApi
Imports SharedLibrary.SharedLibrary
Imports SharedLibrary.SharedLibrary.SharedMethods
Imports SLib = SharedLibrary.SharedLibrary.SharedMethods


Partial Public Class ThisAddIn

    ''' <summary>
    ''' Checks if INI configuration loading has failed and attempts initialization if needed.
    ''' Returns True if INI loading ultimately failed, False if successful.
    ''' </summary>
    Public Function INILoadFail() As Boolean
        If Not INIloaded Then
            If Not StartupInitialized Then
                DelayedStartupTasks()
                RemoveStartupHandlers()
                If Not INIloaded Then Return True
                Return False
            Else
                InitializeConfig(False, False)
                If Not INIloaded Then
                    Return True
                End If
                Return False
            End If
        Else
            Return False
        End If
    End Function


    ''' <summary>
    ''' Manages a custom Word undo record scope for VSTO operations.
    ''' Automatically starts a custom undo record on creation and ends it on disposal.
    ''' Only supported in Word 2013 (version 15.0) and later.
    ''' </summary>
    Friend NotInheritable Class WordUndoScope
        Implements System.IDisposable

        Private ReadOnly _app As Microsoft.Office.Interop.Word.Application
        Private ReadOnly _undo As Microsoft.Office.Interop.Word.UndoRecord
        Private ReadOnly _iStarted As System.Boolean

        ''' <summary>
        ''' Initializes a new undo scope. Starts a custom undo record if Word version supports it
        ''' and no other custom record is currently active.
        ''' </summary>
        ''' <param name="app">The Word Application instance.</param>
        ''' <param name="name">Optional custom name for the undo record. Defaults to "VSTO-Action".</param>
        Public Sub New(app As Microsoft.Office.Interop.Word.Application, Optional name As System.String = Nothing)
            _app = app
            _undo = _app.UndoRecord

            ' Word < 2013 (Version < 15.0) does not have UndoRecord.
            Dim ver As System.Version = New System.Version(_app.Version)
            If ver.Major < 15 Then
                Return
            End If

            ' Only start if no other custom record is currently running.
            If Not _undo.IsRecordingCustomRecord Then
                If name IsNot Nothing AndAlso name.Length > 0 Then
                    _undo.StartCustomRecord(name)
                Else
                    _undo.StartCustomRecord("VSTO-Action")
                End If
                _iStarted = True
            End If
        End Sub

        ''' <summary>
        ''' Ends the custom undo record if it was started by this scope.
        ''' </summary>
        Public Sub Dispose() Implements System.IDisposable.Dispose
            Try
                If _iStarted AndAlso _undo.IsRecordingCustomRecord Then
                    _undo.EndCustomRecord()
                End If
            Catch ex As System.Exception
                ' Do not throw exceptions in Dispose.
            End Try
        End Sub
    End Class

    ''' <summary>
    ''' Replaces each sequence \\X with \\uXXXX (double backslash).
    ''' Example: \\; becomes \\u003B, \\&lt; becomes \\u003C, etc.
    ''' </summary>
    Public Function HideEscape(ByVal input As String) As String
        Return System.Text.RegularExpressions.Regex.Replace(input, "\\\\(.)",
            Function(m As System.Text.RegularExpressions.Match) As String
                Dim c As Char = m.Groups(1).Value(0)
                Dim hex As String = System.Convert.ToInt32(c).ToString("X4")
                Return "\\u" & hex
            End Function)
    End Function

    ''' <summary>
    ''' Replaces each sequence \\uXXXX (double backslash) back to the corresponding character.
    ''' Example: \\u003B becomes ;, \\u003C becomes &lt;, etc.
    ''' </summary>
    Public Function UnHideEscape(ByVal input As String) As String
        Return System.Text.RegularExpressions.Regex.Replace(input, "\\\\u([0-9A-Fa-f]{4})",
            Function(m As System.Text.RegularExpressions.Match) As String
                Dim code As Integer = Integer.Parse(m.Groups(1).Value, System.Globalization.NumberStyles.HexNumber)
                Return System.Convert.ToChar(code).ToString()
            End Function)
    End Function

    ''' <summary>
    ''' Gathers selected Word documents from currently open documents.
    ''' Optionally prompts the user to select one, all, or none.
    ''' Returns formatted document content with XML-style tags, or special values "NONE", empty string, or "ERROR ...".
    ''' </summary>
    ''' <param name="IncludeName">Include document name in output header.</param>
    ''' <param name="IncludeNone">Offer "Do not add any document" option in selection.</param>
    ''' <param name="ExceptCurrent">Exclude the currently active document from the list.</param>
    ''' <param name="SilentAndGetAll">Skip user prompt and return all documents (after optional exclusion).</param>
    Public Function GatherSelectedDocuments(Optional IncludeName As Boolean = True,
                                            Optional IncludeNone As System.Boolean = False,
                                            Optional ExceptCurrent As Boolean = False,
                                            Optional SilentAndGetAll As Boolean = False) As System.String
        Try
            Dim app As Microsoft.Office.Interop.Word.Application = Globals.ThisAddIn.Application

            ' Collect all open documents (unique by FullName/Name to avoid duplicates from multiple windows)
            Dim docList As New System.Collections.Generic.List(Of Microsoft.Office.Interop.Word.Document)()
            Dim seen As New System.Collections.Generic.HashSet(Of System.String)(System.StringComparer.OrdinalIgnoreCase)

            For Each doc As Microsoft.Office.Interop.Word.Document In app.Documents
                Dim key As System.String = If(Not System.String.IsNullOrEmpty(doc.FullName), doc.FullName, doc.Name)
                If Not seen.Contains(key) Then
                    seen.Add(key)
                    docList.Add(doc)
                End If
            Next

            ' Optionally exclude the currently active document
            If ExceptCurrent Then
                Dim activeDoc As Microsoft.Office.Interop.Word.Document = Nothing
                Try
                    activeDoc = app.ActiveDocument
                Catch
                    activeDoc = Nothing
                End Try
                If activeDoc IsNot Nothing Then
                    For i As System.Int32 = docList.Count - 1 To 0 Step -1
                        If System.Object.ReferenceEquals(docList(i), activeDoc) Then
                            docList.RemoveAt(i)
                        End If
                    Next
                End If
            End If

            If docList.Count = 0 Then
                Return "NONE"
            End If

            ' If silent mode requested: return all (after optional exclusion) without prompting
            If SilentAndGetAll Then
                Return BuildDocumentsResult(docList, IncludeName)
            End If

            ' Build selection items for each open document
            Dim selItems As New System.Collections.Generic.List(Of SelectionItem)()
            For i As System.Int32 = 0 To docList.Count - 1
                Dim d As Microsoft.Office.Interop.Word.Document = docList(i)
                selItems.Add(New SelectionItem($"{d.Name} ({d.FullName})", i + 1))
            Next

            ' Add "All open documents" and optional "None"
            Dim indexAll As System.Int32 = selItems.Count + 1
            selItems.Add(New SelectionItem("Add all open documents", indexAll))

            Dim indexNone As System.Int32 = -1
            If IncludeNone Then
                indexNone = indexAll + 1
                selItems.Add(New SelectionItem("Do not add any document", indexNone))
            End If

            ' Prompt user (default/highlight on "All")
            Dim itemsArray As SelectionItem() = selItems.ToArray()
            Dim picked As System.Int32 = SelectValue(itemsArray, indexAll, "Choose document to add …")

            ' User cancelled or invalid choice
            If picked < 1 Then
                Return System.String.Empty
            End If

            ' User explicitly chose "None"
            If IncludeNone AndAlso picked = indexNone Then
                Return System.String.Empty
            End If

            ' Determine targets based on selection
            Dim targets As New System.Collections.Generic.List(Of Microsoft.Office.Interop.Word.Document)()
            If picked = indexAll Then
                targets.AddRange(docList)
            Else
                If picked - 1 >= 0 AndAlso picked - 1 < docList.Count Then
                    targets.Add(docList(picked - 1))
                Else
                    Return System.String.Empty
                End If
            End If

            Return BuildDocumentsResult(targets, IncludeName)

        Catch ex As System.Exception
            Return "ERROR " & ex.Message
        End Try
    End Function

    ''' <summary>
    ''' Builds the concatenated document content string with XML-style tags.
    ''' Each document is wrapped in &lt;DOCUMENTn&gt; tags with optional name header.
    ''' </summary>
    Private Function BuildDocumentsResult(docs As System.Collections.Generic.List(Of Microsoft.Office.Interop.Word.Document),
                                          includeName As System.Boolean) As System.String
        Dim insertedDocuments As System.String = System.String.Empty
        Dim tagIndex As System.Int32 = 1

        For Each d As Microsoft.Office.Interop.Word.Document In docs
            If includeName Then insertedDocuments &= $"Here follows document no. {tagIndex} with the name '" & d.Name & "': " & vbCrLf
            insertedDocuments &= $"<DOCUMENT{tagIndex}>" & vbCrLf
            insertedDocuments &= d.Content.Text & vbCrLf
            insertedDocuments &= $"</DOCUMENT{tagIndex}>" & vbCrLf
            tagIndex += 1
        Next

        If System.String.IsNullOrEmpty(insertedDocuments) Then
            ShowCustomMessageBox("No content could be retrieved from the selected document(s).")
            Return System.String.Empty
        End If

        Return insertedDocuments
    End Function

    ''' <summary>
    ''' Finds long text in the current selection using anchored fast search.
    ''' Delegates to WordSearchHelper.FindLongTextAnchoredFast with optional skip-deleted-text support.
    ''' </summary>
    ''' <param name="findText">The text to find.</param>
    ''' <param name="selection">The Word selection to search within (modified if found).</param>
    ''' <param name="Skipdeleted">If True, skips text marked as deleted revisions.</param>
    ''' <returns>True if text was found, False otherwise.</returns>
    Public Function FindLongTextInChunks(ByVal findText As String, ByRef selection As Word.Selection, Optional Skipdeleted As Boolean = True) As Boolean

        Debug.WriteLine("Entering into FindLongTextAnchoredFast")

        Dim answer As Boolean = WordSearchHelper.FindLongTextAnchoredFast(selection, findText, Skipdeleted)

        Debug.WriteLine("Text found: " & "'" & selection.Text & "'")

        Return answer

    End Function


    ''' <summary>
    ''' Counts real words in the current selection: sequences of letters (Unicode) optionally joined by internal apostrophes or hyphens; numeric/mixed tokens are ignored.
    ''' </summary>
    Private Function GetSelectedTextLength() As Integer
        Try
            Dim wordApp As Microsoft.Office.Interop.Word.Application = Globals.ThisAddIn.Application
            Dim selection As Microsoft.Office.Interop.Word.Selection = wordApp.Selection

            Dim selectedText As String = selection.Text
            If String.IsNullOrWhiteSpace(selectedText) Then
                Return 0
            End If

            ' Pattern:
            ' \b                Word boundary
            ' [\p{L}]+          One or more Unicode letters
            ' (?:[''\-‑–][\p{L}]+)*  Optional internal apostrophe/hyphen/dash + letters (e.g. don't, mother-in-law, rock'n'roll)
            ' \b                Word boundary
            ' Excludes tokens containing digits or starting with punctuation.
            Dim pattern As String = "\b[\p{L}]+(?:[''\-‑–][\p{L}]+)*\b"

            Return Regex.Matches(selectedText, pattern).Count
        Catch ex As System.Exception
            Return 0
        End Try
    End Function

    ''' <summary>
    ''' Replaces placeholders in the template string with values from instance fields or properties.
    ''' Placeholders use {FieldName} or {PropertyName} syntax. Sensitive placeholders like {Codebasis}
    ''' and {INI_*API*} are cleared for security reasons.
    ''' </summary>
    ''' <param name="template">The template string containing placeholders.</param>
    ''' <returns>The interpolated string with placeholders replaced by actual values.</returns>
    Public Function InterpolateAtRuntime(ByVal template As String) As String
        If template Is Nothing Then
            MessageBox.Show("Error InterpolateAtRuntime: Template is Nothing.", "Error", MessageBoxButtons.OK, MessageBoxIcon.Error)
            Return ""
        End If

        ' Clear sensitive placeholders
        template = Regex.Replace(template, "{Codebasis}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_DecodedAPI}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_DecodedAPI_2}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_APIKey}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_APIKeyBack}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_APIKey_2}", "", RegexOptions.IgnoreCase)
        template = Regex.Replace(template, "{INI_APIKeyBack_2}", "", RegexOptions.IgnoreCase)

        Dim result As String = template

        Dim placeholderPattern As String = "\{([^}]+)\}"
        Dim matches As MatchCollection = Regex.Matches(template, placeholderPattern)

        For Each m As Match In matches
            Dim placeholder As String = m.Value          ' e.g. "{Name}"
            Dim varName As String = m.Groups(1).Value    ' e.g. "Name"

            ' Search for Field
            Dim fieldInfo = Me.GetType().GetField(varName)
            If fieldInfo IsNot Nothing Then
                Dim fieldValue = fieldInfo.GetValue(Me)
                If fieldValue IsNot Nothing Then
                    result = result.Replace(placeholder, fieldValue.ToString())
                End If
                Continue For
            End If

            ' Search for Property
            Dim propInfo = Me.GetType().GetProperty(varName)
            If propInfo IsNot Nothing Then
                Dim propValue = propInfo.GetValue(Me)
                If propValue IsNot Nothing Then
                    result = result.Replace(placeholder, propValue.ToString())
                End If
            End If
        Next

        Return result
    End Function


    ''' <summary>
    ''' Lightweight IDisposable scope to show a modeless progress window with cancellation support
    ''' for long-running operations. Integrates with ProgressBarModule and DPIProgressForm/ProgressForm.
    ''' The progress form runs on a dedicated STA UI thread with its own message loop.
    ''' </summary>
    Public NotInheritable Class ProgressScope
        Implements IDisposable

        Private ReadOnly _cts As System.Threading.CancellationTokenSource = New System.Threading.CancellationTokenSource()
        Private _uiThread As System.Threading.Thread
        Private _form As System.Windows.Forms.Form
        Private ReadOnly _useDpiForm As Boolean
        Private _closed As Integer = 0

        ''' <summary>
        ''' Initializes the progress scope and displays a modeless progress window.
        ''' The form updates its display by polling ProgressBarModule.* every 250ms.
        ''' </summary>
        ''' <param name="headerText">The header text displayed on the progress form.</param>
        ''' <param name="initialLabel">The initial status label text.</param>
        ''' <param name="max">Maximum progress value (default 100).</param>
        ''' <param name="useDpiForm">If True, uses DPIProgressForm; otherwise uses ProgressForm.</param>
        Public Sub New(headerText As String,
                   initialLabel As String,
                   Optional max As Integer = 100,
                   Optional useDpiForm As Boolean = True)
            _useDpiForm = useDpiForm

            ' Initialize global progress state
            ProgressBarModule.CancelOperation = False
            ProgressBarModule.GlobalProgressMax = System.Math.Max(1, max)
            ProgressBarModule.GlobalProgressValue = 0
            ProgressBarModule.GlobalProgressLabel = If(initialLabel, "")

            ' Spin up a dedicated STA UI thread with its own message loop for the progress form
            _uiThread = New System.Threading.Thread(
            Sub()
                Try
                    System.Windows.Forms.Application.EnableVisualStyles()
                    _form = If(_useDpiForm,
                               CType(New DPIProgressForm(headerText, initialLabel), System.Windows.Forms.Form),
                               CType(New ProgressForm(headerText, initialLabel), System.Windows.Forms.Form))

                    ' Run form (timer inside form pulls ProgressBarModule.* and closes itself when CancelOperation=True)
                    System.Windows.Forms.Application.Run(_form)
                Catch
                    ' Swallow exceptions — cleanup is always attempted in Dispose.
                End Try
            End Sub
        )
            _uiThread.IsBackground = True
            _uiThread.SetApartmentState(System.Threading.ApartmentState.STA)
            _uiThread.Start()
        End Sub

        ''' <summary>
        ''' Reports progress in a thread-safe manner via the global ProgressBarModule.
        ''' </summary>
        ''' <param name="current">Current progress value.</param>
        ''' <param name="max">Optional new maximum value (if >= 1).</param>
        ''' <param name="label">Optional new status label text.</param>
        Public Shared Sub Report(current As Integer,
                             Optional max As Integer = -1,
                             Optional label As String = Nothing)
            If max >= 1 Then ProgressBarModule.GlobalProgressMax = max
            If label IsNot Nothing Then ProgressBarModule.GlobalProgressLabel = label
            ProgressBarModule.GlobalProgressValue = System.Math.Max(0, System.Math.Min(current, ProgressBarModule.GlobalProgressMax))
        End Sub

        ''' <summary>
        ''' Requests cancellation of the operation.
        ''' Also triggered by the Cancel button in the progress UI.
        ''' </summary>
        Public Sub RequestCancel()
            _cts.Cancel()
            ProgressBarModule.CancelOperation = True
        End Sub

        ''' <summary>
        ''' Gets a value indicating whether cancellation has been requested.
        ''' Check this frequently at safe points (between steps/chunks) to bail out early.
        ''' </summary>
        Public ReadOnly Property CancelRequested As Boolean
            Get
                Return ProgressBarModule.CancelOperation OrElse _cts.IsCancellationRequested
            End Get
        End Property

        ''' <summary>
        ''' Gets the CancellationToken associated with this scope.
        ''' </summary>
        Public ReadOnly Property Token As System.Threading.CancellationToken
            Get
                Return _cts.Token
            End Get
        End Property

        ''' <summary>
        ''' Disposes the progress scope, signaling cancellation and closing the progress form.
        ''' Waits up to 1 second for the UI thread to exit cleanly.
        ''' </summary>
        Public Sub Dispose() Implements IDisposable.Dispose
            If System.Threading.Interlocked.Exchange(_closed, 1) <> 0 Then Return
            Try
                ' Signal cancel so the form's timer path also closes itself
                ProgressBarModule.CancelOperation = True

                Dim f = _form
                If f IsNot Nothing Then
                    Try
                        If f.IsHandleCreated AndAlso Not f.IsDisposed Then
                            ' Request close on the form's thread
                            f.BeginInvoke(New System.Action(Sub()
                                                                Try
                                                                    If Not f.IsDisposed Then f.Close()
                                                                Catch
                                                                End Try
                                                            End Sub))
                        End If
                    Catch
                        ' Ignore cross-thread or shutdown races
                    End Try
                End If
            Finally
                ' Give the UI thread a moment to exit cleanly
                Try
                    If _uiThread IsNot Nothing AndAlso _uiThread.IsAlive Then
                        If Not _uiThread.Join(1000) Then
                            Try : _uiThread.Interrupt() : Catch : End Try
                        End If
                    End If
                Catch
                End Try
            End Try
        End Sub
    End Class

    ''' <summary>
    ''' Retrieves the display name of the Word user interface language.
    ''' Falls back to "English" if an error occurs.
    ''' </summary>
    Public Function GetWordDefaultInterfaceLanguage() As String
        Try
            ' Get the language ID of the Word user interface
            Dim uiLanguageID As Integer = Globals.ThisAddIn.Application.LanguageSettings.LanguageID(MsoAppLanguageID.msoLanguageIDUI)

            ' Convert the language ID to a human-readable name
            Dim cultureInfo As Globalization.CultureInfo = New Globalization.CultureInfo(uiLanguageID)

            ' Return the language display name
            Return cultureInfo.DisplayName
        Catch ex As System.Exception
            Return "English"
        End Try
    End Function

    ''' <summary>
    ''' Encodes (encrypts) an API key using a user-provided secret key.
    ''' Optionally handles a prefix (e.g., "sk-") that is preserved during encoding.
    ''' Prompts the user for the prefix and secret key via input dialogs.
    ''' </summary>
    ''' <param name="apiKey">The API key to encode.</param>
    ''' <returns>The encoded API key with prefix (if applicable), or "Error" on failure.</returns>
    Private Function CodeAPIKey(ByVal apiKey As String) As String
        Dim modifiedKey As String
        Dim resultKey As String
        Dim xcodebasis As String
        Dim HadPrefix As Boolean = False

        Dim PrefixValue As String = INI_APIKeyPrefix

        ' Check if an API key is provided
        apiKey = apiKey.Trim()
        If String.IsNullOrEmpty(apiKey) Then
            ShowCustomMessageBox("No text selected to encode. Select the API Key you wish to encode.")
            Return "Error"
        End If

        PrefixValue = SLib.ShowCustomInputBox("Please enter the API key prefix (as used in the configuration file, if any):", "API Key Encryptor", True, PrefixValue)

        xcodebasis = SLib.ShowCustomInputBox("Please enter the secret key:", "API Key Encryptor", True)
        If String.IsNullOrEmpty(xcodebasis) Then
            ShowCustomMessageBox("No secret key entered.")
            Return "Error"
        End If

        ' Check if the API key has the prefix
        If Not String.IsNullOrEmpty(PrefixValue) AndAlso apiKey.StartsWith(PrefixValue) Then
            HadPrefix = True
            ' Encrypt only the part after the prefix
            modifiedKey = apiKey.Substring(PrefixValue.Length)
        Else
            ' Encrypt the entire key if no prefix is present
            modifiedKey = apiKey
        End If

        ' Encrypt the modified key (without the prefix)
        resultKey = CodeString(modifiedKey, xcodebasis)

        ' Add the prefix back if it was present
        If HadPrefix Then
            resultKey = PrefixValue & resultKey
        End If

        Return resultKey
    End Function

    ''' <summary>
    ''' Decodes (decrypts) an API key using a user-provided secret key.
    ''' Optionally handles a prefix (e.g., "sk-") that is preserved during decoding.
    ''' Prompts the user for the prefix and secret key via input dialogs.
    ''' </summary>
    ''' <param name="apiKey">The encoded API key to decode.</param>
    ''' <returns>The decoded API key with prefix (if applicable), or "Error" on failure.</returns>
    Private Function DeCodeAPIKey(ByVal apiKey As String) As String
        Dim modifiedKey As String
        Dim resultKey As String
        Dim xcodebasis As String

        Dim PrefixValue As String = INI_APIKeyPrefix

        ' Check if an API key is provided
        apiKey = apiKey.Trim()
        If String.IsNullOrEmpty(apiKey) Then
            ShowCustomMessageBox("No text selected to decode. Select the API Key you wish to decode.")
            Return "Error"
        End If

        PrefixValue = SLib.ShowCustomInputBox("Please enter the API key prefix (as used in the configuration file, if any):", "API Key Decryptor", True, PrefixValue)

        xcodebasis = SLib.ShowCustomInputBox("Please enter the secret key:", "API Key Decryptor", True)
        If String.IsNullOrEmpty(xcodebasis) Then
            ShowCustomMessageBox("No secret key entered.")
            Return "Error"
        End If

        ' Check if the key starts with the prefix
        If Not String.IsNullOrEmpty(PrefixValue) AndAlso apiKey.StartsWith(PrefixValue) Then
            ' Decrypt only the part after the prefix
            modifiedKey = apiKey.Substring(PrefixValue.Length)
        Else
            ' Decrypt the entire key if no prefix is present
            modifiedKey = apiKey
        End If

        ' Decrypt the modified key (without the prefix)
        resultKey = DecodeString(modifiedKey, xcodebasis)

        ' Add the prefix back only if it was in the original key
        If Not String.IsNullOrEmpty(PrefixValue) AndAlso apiKey.StartsWith(PrefixValue) Then
            resultKey = PrefixValue & resultKey
        End If

        Return resultKey
    End Function

    ''' <summary>
    ''' Validates that the VBA helper module is working and meets the minimum required version.
    ''' Calls the VBA function "CheckAppHelper" and compares its version number.
    ''' </summary>
    ''' <returns>True if the VBA module version meets MinHelperVersion, False otherwise.</returns>
    Public Function VBAModuleWorking() As Boolean

        Dim xlApp As Microsoft.Office.Interop.Word.Application = Me.Application

        Try
            ' Call the VBA function
            Dim HelperVersion As Integer = CType(xlApp.Run("CheckAppHelper"), Integer)

            If HelperVersion >= MinHelperVersion Then
                Return True
            Else
                Return False
            End If
        Catch ex As Exception
            ' Return False if VBA call fails
            Return False
        End Try

    End Function

    ''' <summary>
    ''' Win32 API import: Retrieves the asynchronous state of a specified virtual key.
    ''' Used for detecting key presses (e.g., Escape key) outside normal event flow.
    ''' </summary>
    ''' <param name="vKey">The virtual-key code to check.</param>
    ''' <returns>Non-zero if the key is pressed, zero otherwise.</returns>
    <System.Runtime.InteropServices.DllImport("user32.dll",
    SetLastError:=True, CharSet:=System.Runtime.InteropServices.CharSet.Auto)>
    Public Shared Function GetAsyncKeyState(ByVal vKey As System.Int32) As System.Int16
    End Function

    ''' <summary>
    ''' Virtual-key code for the Escape key (0x1B).
    ''' Used with GetAsyncKeyState for cancellation detection.
    ''' </summary>
    Private Const VK_ESCAPE As System.Int32 = &H1B



End Class